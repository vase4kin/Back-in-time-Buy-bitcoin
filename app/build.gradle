/*
 * Copyright 2021  Andrey Tolpeev
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import app.buildSrc.Libs
import app.buildSrc.Config

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'io.gitlab.arturbosch.detekt'
apply plugin: 'dagger.hilt.android.plugin'

android {
    defaultConfig {
        applicationId Config.applicationId
        versionCode Config.versionCode
        versionName Config.versionName
    }

    signingConfigs {
        release {
            storeFile file(KEYSTORE_FILE)
            storePassword KEYSTORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix = ".debug"
        }
    }

    productFlavors {
    }

    buildFeatures {
        dataBinding = true
    }

    compileOptions {
        //Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dependencies {
        coreLibraryDesugaring Libs.coreLibraryDesugaring
    }

    lintOptions {
        xmlReport false
        abortOnError true
    }
}

dependencies {
    implementation Libs.Google.Material.material
    implementation Libs.Google.AndroidX.appCompat
    implementation Libs.Google.AndroidX.constraintLayout
    implementation Libs.Google.AndroidX.lifecycleRuntimeKtx
}

dependencies {
    implementation Libs.Dagger.hilt
    kapt Libs.Dagger.hiltCompiler
}

kapt {
    correctErrorTypes true
}

dependencies {
    implementation Libs.androidGifDrawable
    testImplementation Libs.Test.jUnit
}

dependencies {
    implementation project(Libs.Local.crashlytics)
    implementation project(Libs.Local.remoteConfig)
    implementation project(Libs.Shared.timeTravelMachine)
    implementation project(Libs.Shared.tracker)
}

dependencies {
    // Firebase dependencies
    implementation platform(Libs.Google.Firebase.bom)

    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation "com.google.firebase:firebase-core"
    implementation "com.google.firebase:firebase-ads"
    implementation "com.google.firebase:firebase-config-ktx"
}

dependencies {
    implementation Libs.Kotlin.stdlib
    implementation Libs.Kotlin.coroutinesAndroid
}

apply plugin: 'com.google.gms.google-services'
